concept label [Item, Label]
purpose organize items into overlapping categories
state
  labels: Item -> set Label 
actions 
  affix(i: Item, l : Label)
    i.labels += l
  detach(i : Item, l : Label)
    i.labels -= l
  find(l: Label) : Item 
    l.~labels 
  clear(i : Item)
    i.labels := {}
principle
  affix(i,l) then i in find(l) until detach(i,l) or clear(i),
  no affix(i,l) or detach(i,l) then i not in find(l)

concept todo 
purpose keep track of tasks
state 
  done, pending: set Task
actions
  add(t : Task)
    when t not in done+pending
      pending += t
  delete(t : Task)
    when t in done+pending 
      done -= t
      pending -= t
  complete(t : Task)
    when t in pending 
      pending -= t
      done += t
principle
  add(t) then t in pending until delete(t) or complete(t),
  complete(t) then t in done until delete(t)

app todo_label 
include 
  todo 
  label [todo.Task, String]
sync todo.delete(t)
  label.clear(t)
sync todo.add(t)
  label.affix(t, "pending")
sync todo.complete(t)
  label.detach(t, "pending")
sync label.detach(t, "pending")
  todo.complete(t)
sync label.affix(t, "pending")
  todo.add(t)



  